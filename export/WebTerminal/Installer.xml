<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for Windows (x86-64) 2015.3 (Build 215U)" ts="2015-12-15 14:55:07">
<Class name="WebTerminal.Installer">
<Description>
Importing this class will install Cache WEB Terminal properly.</Description>
<CompileAfter>Engine,Router,StaticContent</CompileAfter>
<Super>%Projection.AbstractProjection</Super>
<TimeChanged>63898,2021.685611</TimeChanged>
<TimeCreated>63890,71053.144208</TimeCreated>

<Parameter name="DispatchClass">
<Default>WebTerminal.Router</Default>
</Parameter>

<Projection name="Reference">
<Type>Installer</Type>
</Projection>

<Method name="CreateProjection">
<Description>
This method is invoked when a class is compiled.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[cls:%String,&params]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    set ns = $NAMESPACE
    zn:ns'="%SYS" "%SYS"
    do ##class(Security.System).GetInstallationSecuritySetting(.security)
    set cspProperties("AutheEnabled") = 32 // password by default, 64 for no password
    set cspProperties("NameSpace") = ns // terminal works only in %SYS namespace, only in % package
    set cspProperties("Description") = "A WEB application for Cache WEB Terminal."
    set cspProperties("IsNameSpaceDefault") = 0
    set cspProperties("DispatchClass") = ..#DispatchClass
    if ('##class(Security.Applications).Exists("/terminal")) {
        w !, "Creating WEB application ""/terminal""..."
        set tSC = ##class(Security.Applications).Create("/terminal", .cspProperties)
        if $$$ISERR(tSC) throw ##class(%Installer.Exception).CreateFromStatus(tSC)
        w !, "WEB application ""/terminal"" is created."
        //set ^SYS("Security", "CSP", "AllowClass", "/terminal/", ..#DispatchClass) = 1
    } else {
        do ##class(Security.Applications).Get("/terminal", .props)
        if (props("DispatchClass") '= ..#DispatchClass) {
            w !, "WARNING! WEB application ""/terminal"" exists but does not refer to ",
                DispatchClass, ".", !, "Please, set correct dispatch class for this application ",
                "or create a terminal WEB-application manually."
        } else {
            w !, "WEB application ""/terminal"" already exists, so it is ready to use."
        }
    }
    set status = ##Class(Config.Namespaces).Get("%All")
    if ($$$ISERR(status)) { // no namespace %All
        w !, "Creating %All namespace..."
        set Properties("Globals") = "CACHETEMP"
        set Properties("Library") = "CACHELIB"
        set Properties("Routines") = "CACHETEMP"
        set Properties("SysGlobals") = "CACHESYS"
        set Properties("SysRoutines") = "CACHESYS"
        set Properties("TempGlobals") = "CACHETEMP"
        set status = ##Class(Config.Namespaces).Create("%All", .Properties)
        if ($$$ISERR(status)) {
            do $System.Status.DisplayError(status)
            quit status
        } else {
            w !, "%All namespace is created."
        }
    }
    w !, "Mapping %WebTerminal package into all namespaces:"
    set mapTo = $LISTBUILD("%All", "SAMPLES", "DOCBOOK")
    do ##Class(Config.Namespaces).Get("USER", .InstallNSProps)
    set Properties("Database") = $get(InstallNSProps("Routines"))
    set ptr = 0
    while $LISTNEXT(mapTo, ptr, value) {
        continue:(ns = value)
        w " ", value
        set status = ##Class(Config.MapPackages).Create(value, "WebTerminal", .Properties)
        if ($$$ISERR(status)) {
            do $SYSTEM.Status.DisplayError(status)
        }
    }
    w ".", !, "Mapping complete."
    w !, "WebTerminal package successfully mapped into all namespaces."
    zn:ns'="%SYS" ns
    quit $$$OK
]]></Implementation>
</Method>

<Method name="RemoveProjection">
<Description>
This method is invoked when a class is 'uncompiled'.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[cls:%String,&params,recompile:%Boolean]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    set ns = $NAMESPACE
    zn:ns'="%SYS" "%SYS"
    if (##class(Security.Applications).Exists("/terminal")) {
        do ##class(Security.Applications).Get("/terminal", .props)
        if (props("DispatchClass") '= ..#DispatchClass) {
            w !, "Won't remove WEB-application ""/terminal"" because it does not refer to ",
                "dispatch class anymore."
        } else {
            w !, "Deleting WEB application ""/terminal""..."
            do ##class(Security.Applications).Delete("/terminal")
            kill ^SYS("Security", "CSP", "AllowClass", "/terminal/", ..#DispatchClass)
            w !, "WEB application ""/terminal"" was successfully removed."
        }
    }
    w !, "Unmapping %WebTerminal package from all namespaces:"
    set mapTo = $LISTBUILD("%All", "SAMPLES", "DOCBOOK")
    set ptr = 0
    while $LISTNEXT(mapTo, ptr, value) {
        continue:(ns = value)
        w " ", value
        set status = ##Class(Config.MapPackages).Delete(value, "WebTerminal")
        if ($$$ISERR(status)) {
            do $SYSTEM.Status.DisplayError(status)
        }
    }
    w ".", !, "Unmapping complete."
    kill ^%WebTerminal.Autocomplete
    zn:ns'="%SYS" ns
    QUIT $$$OK
]]></Implementation>
</Method>
</Class>
</Export>
